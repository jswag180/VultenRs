#version 450
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_control_flow_attributes : enable

#include "prelude.h"

layout(local_size_x_id = 0, local_size_y = 1, local_size_z = 1) in;

#define NHWC 0
#define NCHW 1
#if FORMAT == NHWC
layout(set = 0, binding = 0) buffer a { readonly  TYPE_P_0 inData[BTACH_SIZE][HEIGHT_IN][WIDTH_IN][DEPTH_IN]; };
#else
layout(set = 0, binding = 0) buffer a { readonly  TYPE_P_0 inData[BTACH_SIZE][DEPTH_IN][HEIGHT_IN][WIDTH_IN]; };
#endif
layout(set = 0, binding = 1) buffer b { writeonly TYPE_P_0 outData[]; };

layout(constant_id = 1) const uint stride_h = 0;
layout(constant_id = 2) const uint stride_w = 0;
layout(constant_id = 3) const uint dilation_h = 0;
layout(constant_id = 4) const uint dilation_w = 0;
layout(constant_id = 5) const uint padding_h = 0;
layout(constant_id = 6) const uint padding_w = 0;

layout(push_constant) uniform PushConstants {
	uint start;
    uint stop;
    uint channel;
} push;

void main() {
    uint index = gl_GlobalInvocationID.x + push.start;

    if(index > push.stop){
        return;
    }

    uint batch_total_size = HEIGHT_OUT * WIDTH_OUT; 
    uint batch = index / batch_total_size;
    uint offset_index = (index - (batch * batch_total_size));
    uint x = offset_index / WIDTH_OUT;
    uint y = offset_index % WIDTH_OUT;

    uint stridedX = x * stride_h;
    uint stridedY = y * stride_w;
    uint filter_offset = index * FILTER_HEIGHT * FILTER_WIDTH;
    for(uint fx = 0; fx < FILTER_HEIGHT; fx++){
        for(uint fy = 0; fy < FILTER_WIDTH; fy++){
            uint xIdx = stridedX + (fx * dilation_h) - padding_h;
            uint yIdx = stridedY + (fy * dilation_w) - padding_w;

            [[unroll]] for(uint c = 0; c < DEPTH_IN; c++){
                if(xIdx < HEIGHT_IN && yIdx < WIDTH_IN && xIdx >= 0 && yIdx >= 0){
                    #if FORMAT == NHWC
                    outData[((filter_offset + (fx * FILTER_HEIGHT) + fy) * DEPTH_IN) + c] = inData[batch][xIdx][yIdx][c];
                    #else
                    outData[((filter_offset + (fx * FILTER_HEIGHT) + fy) * DEPTH_IN) + c] = inData[batch][c][xIdx][yIdx];
                    #endif
                }else{
                    outData[((filter_offset + (fx * FILTER_HEIGHT) + fy) * DEPTH_IN) + c] = TYPE_0(0);
                }
            }
        }
    }
}